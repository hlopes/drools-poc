package org.drools.poc.fire

import org.drools.poc.fire.Fire
import org.drools.poc.fire.Sprinkler
import org.drools.poc.fire.Alarm
import org.drools.poc.LoggingUtil

rule "When there is a fire turn on the sprinkler"
when
    Fire($room : room)
    $sprinkler : Sprinkler(room == $room, on == false)
then
    modify($sprinkler) { setOn(true) };
end

rule "Raise the alarm when we have one or more fires"
when
    exists Fire()
then
    insert(new Alarm());
end

rule "Cancel the alarm when all the fires have gone"
when
    not Fire()
    $alarm : Alarm()
then
    delete($alarm);
end


rule "Status output when things are ok"
when
    not Alarm()
    not Sprinkler( on == true )
then
    LoggingUtil.info( "Everything is ok" );
end